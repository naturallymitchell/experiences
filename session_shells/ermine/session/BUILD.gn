# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/dart/dart_component.gni")
import("//build/dart/dart_library.gni")
import("//src/session/build/session_manager.gni")

dart_library("lib") {
  package_name = "workstation_session"
  source_dir = "lib"
  null_safe = true
  sources = [ "main.dart" ]
  deps = [
    "//sdk/dart/fidl",
    "//sdk/dart/fuchsia",
    "//sdk/dart/fuchsia_logger",
    "//sdk/dart/fuchsia_services",
    "//sdk/dart/fuchsia_vfs",
    "//sdk/dart/zircon",
    "//sdk/fidl/fuchsia.component",
    "//sdk/fidl/fuchsia.component.decl",
    "//sdk/fidl/fuchsia.io",
    "//sdk/fidl/fuchsia.session.scene",
    "//sdk/fidl/fuchsia.ui.app",
    "//sdk/fidl/fuchsia.ui.focus",
    "//sdk/fidl/fuchsia.ui.input",
    "//sdk/fidl/fuchsia.ui.keyboard.focus",
    "//sdk/fidl/fuchsia.ui.shortcut",
    "//sdk/fidl/fuchsia.ui.views",
  ]
}

dart_component("session_component") {
  component_name = "workstation_session"
  manifest = "meta/workstation_session.cml"
  deps = [ ":lib" ]
}

_variants = [
  "jit",
  "jit_product",
  "aot",
  "aot_product",
]

pkg_deps = []
foreach(variant, _variants) {
  fuchsia_component("${variant}_routing_component") {
    component_name = "workstation_routing_${variant}"
    manifest = "meta/workstation_routing_${variant}.cml"
  }

  pkg_deps += [ ":${variant}_routing_component" ]
}

fuchsia_package("workstation_session_pkg") {
  package_name = "workstation_session"

  deps = pkg_deps
  deps += [ ":session_component" ]
}

session_manager_package("session_manager_jit") {
  config =
      "//src/experiences/session_shells/ermine/session/session_config_jit.json"
}

session_manager_package("session_manager_jit_product") {
  config = "//src/experiences/session_shells/ermine/session/session_config_jit_product.json"
}

session_manager_package("session_manager_aot") {
  config =
      "//src/experiences/session_shells/ermine/session/session_config_aot.json"
}

session_manager_package("session_manager_aot_product") {
  config = "//src/experiences/session_shells/ermine/session/session_config_aot_product.json"
}

# TODO(fxbug.dev/91803): Remove this after transitioning the workstation product configuration to
# one of the above.
group("session_manager") {
  if (!dart_default_build_cfg.is_aot && !dart_default_build_cfg.is_product) {
    public_deps = [ ":session_manager_jit" ]
  } else if (!dart_default_build_cfg.is_aot &&
             dart_default_build_cfg.is_product) {
    public_deps = [ ":session_manager_jit_product" ]
  } else if (dart_default_build_cfg.is_aot &&
             !dart_default_build_cfg.is_product) {
    public_deps = [ ":session_manager_aot" ]
  } else if (dart_default_build_cfg.is_aot &&
             dart_default_build_cfg.is_product) {
    public_deps = [ ":session_manager_aot_product" ]
  }
}

group("session") {
  public_deps = [ ":workstation_session_pkg" ]
}
