# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/testing/environments.gni")
import("//topaz/runtime/dart/dart_fuchsia_test.gni")
import("//topaz/runtime/dart/flutter_test.gni")
import("//topaz/runtime/flutter_runner/flutter_app.gni")

flutter_app("simple_browser") {
  main_dart = "lib/main.dart"
  package_name = "simple_browser"

  meta = [
    {
      path = rebase_path("meta/simple_browser.cmx")
      dest = "simple_browser.cmx"
    },
  ]

  manifest = "pubspec.yaml"

  sources = [
    "src/blocs/tabs_bloc.dart",
    "src/models/tabs_action.dart",
  ]

  deps = [
    "//sdk/fidl/fuchsia.web",
    "//third_party/dart-pkg/git/flutter/packages/flutter",
    "//third_party/dart-pkg/pub/html_unescape",
    "//third_party/dart-pkg/pub/http",
    "//topaz/public/dart/fuchsia_logger",
    "//topaz/public/dart/fuchsia_modular",
    "//topaz/public/dart/fuchsia_scenic_flutter",
    "//topaz/public/dart/widgets:lib.widgets",
    "//topaz/public/lib/webview",
  ]
}

# fx run-host-tests simple_browser_unittests
flutter_test("simple_browser_unittests") {
  sources = [
    "simple_browser_test.dart",
  ]

  deps = [
    ":simple_browser_dart_library",
    "//sdk/fidl/fuchsia.web",
    "//third_party/dart-pkg/git/flutter/packages/flutter_test",
    "//third_party/dart-pkg/pub/mockito",
    "//third_party/dart-pkg/pub/test",
  ]
}

# fx run-test simple_browser_target_test
dart_fuchsia_test("simple_browser_target_test") {
  meta = [
    {
      path = rebase_path("meta/simple_browser_target_test.cmx")
      dest = "simple_browser_target_test.cmx"
    },
  ]

  sources = [
    "simple_browser_target_test.dart",
  ]

  deps = [
    ":simple_browser_dart_library",
    "//third_party/dart-pkg/git/flutter/packages/flutter_test",
    "//third_party/dart-pkg/pub/test",
  ]

  environments = basic_envs
}
