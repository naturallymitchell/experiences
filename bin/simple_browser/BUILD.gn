# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/dart/dart_library.gni")
import("//build/dart/fidl_move.gni")
import("//build/fidl/fidl.gni")
import("//build/flutter/flutter_component.gni")
import("//build/flutter/flutter_test_component.gni")
import("//build/testing/environments.gni")
import("//build/testing/flutter_driver.gni")
import("//src/sys/build/components.gni")
import("//src/sys/build/fuchsia_test_package.gni")
import("//topaz/runtime/dart/flutter_test.gni")

dart_library("lib") {
  package_name = "simple_browser"

  sources = [
    "app.dart",
    "create_web_context.dart",
    "main.dart",
    "src/blocs/tabs_bloc.dart",
    "src/blocs/webpage_bloc.dart",
    "src/models/app_model.dart",
    "src/models/tabs_action.dart",
    "src/models/webpage_action.dart",
    "src/services/simple_browser_navigation_event_listener.dart",
    "src/services/simple_browser_web_service.dart",
    "src/utils/browser_shortcuts.dart",
    "src/utils/sanitize_url.dart",
    "src/utils/tld_checker.dart",
    "src/utils/tlds_provider.dart",
    "src/utils/valid_tlds.dart",
    "src/widgets/error_page.dart",
    "src/widgets/history_buttons.dart",
    "src/widgets/navigation_bar.dart",
    "src/widgets/navigation_field.dart",
    "src/widgets/tabs_widget.dart",
  ]

  deps = [
    "//sdk/fidl/fuchsia.intl",
    "//sdk/fidl/fuchsia.io",
    "//sdk/fidl/fuchsia.ui.shortcut",
    "//sdk/fidl/fuchsia.ui.views",
    "//sdk/fidl/fuchsia.web",
    "//src/experiences/bin/simple_browser_internationalization:internationalization",
    "//src/experiences/session_shells/ermine/keyboard_shortcuts",
    "//third_party/dart-pkg/git/flutter/packages/flutter",
    "//third_party/dart-pkg/git/flutter/packages/flutter_localizations",
    "//third_party/dart-pkg/pub/html_unescape",
    "//third_party/dart-pkg/pub/http",
    "//third_party/dart-pkg/pub/intl",
    "//third_party/dart-pkg/pub/meta",
    "//topaz/public/dart/fuchsia_internationalization_flutter",
    "//topaz/public/dart/fuchsia_logger",
    "//topaz/public/dart/fuchsia_scenic_flutter",
    "//topaz/public/dart/fuchsia_services",
    dart_package_label.fidl,
    dart_package_label.zircon,
  ]
}

resource("keyboard-shortcuts") {
  sources = [ "config/keyboard_shortcuts.json" ]
  outputs = [ "keyboard_shortcuts.json" ]
}

flutter_component("component") {
  component_name = "simple-browser"
  manifest = "meta/simple_browser.cmx"
  deps = [
    ":keyboard-shortcuts",
    ":lib",
  ]
}

fuchsia_package("simple-browser") {
  deps = [ ":component" ]
}

# fx run-host-tests simple_browser_unittests
flutter_test("simple_browser_unittests") {
  sources = [
    "browser_shortcuts_test.dart",
    "sanitize_url_test.dart",
    "simple_browser_test.dart",
    "tabs_bloc_test.dart",
    "tld_checker_test.dart",
    "webpage_bloc_test.dart",
    "widgets/error_page_test.dart",
    "widgets/history_buttons_test.dart",
    "widgets/navigation_bar_test.dart",
    "widgets/navigation_field_test.dart",
    "widgets/tabs_widget_test.dart",
  ]

  deps = [
    ":lib",
    "//sdk/fidl/fuchsia.ui.shortcut",
    "//sdk/fidl/fuchsia.web",
    "//third_party/dart-pkg/git/flutter/packages/flutter",
    "//third_party/dart-pkg/git/flutter/packages/flutter_test",
    "//third_party/dart-pkg/pub/mockito",
    "//third_party/dart-pkg/pub/test",
    "//topaz/public/dart/fuchsia_logger",
  ]
}

flutter_test_component("target-test-component") {
  component_name = "simple-browser-target-test"
  manifest = "meta/simple_browser_target_test.cmx"
  sources = [ "simple_browser_target_test.dart" ]

  deps = [
    ":lib",
    "//third_party/dart-pkg/git/flutter/packages/flutter_test",
    "//third_party/dart-pkg/pub/test",
  ]
}

# fx test simple-browser-target-test
fuchsia_test_package("simple-browser-target-test") {
  test_components = [ ":target-test-component" ]
  test_specs = {
    environments = basic_envs
  }
}

config_data("web_engine_config") {
  for_pkg = "web_engine"

  outputs = [ "config.json" ]

  sources = [ "config/web_engine.json" ]
}
